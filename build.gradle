plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    }

    processResources {
        def expandProps = [
                // Project
                project_version: project_version,
                project_group: project_group,
                project_homepage: project_homepage,
                project_repo: project_repo,
                project_issues: project_issues,

                // Mod Options
                mod_name: mod_name,
                mod_id: mod_id,
                mod_author: mod_author,
                mod_license: mod_license,
                mod_description: mod_description,
                mod_credits: mod_credits,

                // Common
                minecraft_version: minecraft_version,

                // Forge
                forge_version: forge_version,
                forge_loader_version_range: forge_loader_version_range,
                forge_version_range: forge_version_range,
                forge_minecraft_version_range: forge_minecraft_version_range,

                // Fabric
                fabric_entrypoint: fabric_entrypoint,
                fabric_api_version: fabric_api_version,
                fabric_loader_version: fabric_loader_version,

                // Quilt
                quilt_entrypoint: quilt_entrypoint,
                quilt_loader_version: quilt_loader_version,
                quilt_fabric_api_version: quilt_fabric_api_version
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'quilt.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand(expandProps)
        }

        inputs.properties(expandProps)
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = mod_id
    }

    version = project_version
    group = project_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
